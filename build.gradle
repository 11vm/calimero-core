buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-SNAPSHOT'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.0.0-SNAPSHOT'
ext.junitPlatformVersion = '1.0.0-SNAPSHOT'

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin:'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.github.calimero'
version = '2.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]


mainClassName = 'tuwien.auto.calimero.Settings'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['test']
        resources.srcDirs = ['test/resources']
    }
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

artifacts {
    archives testJar
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

junitPlatform {
	details 'none' // 'tree'
    filters {
        tags {
            exclude 'ft12'
            exclude 'slow'
//            exclude 'knxnetip'
        }
    }
}

dependencies {
    api 'org.slf4j:slf4j-api:1.8.0-alpha2'
    api 'org.usb4java:usb4java-javax:1.2.0'
    compileOnly 'javax.xml.stream:stax-api:1.0-2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testImplementation("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")
    testRuntimeOnly 'junit:junit:4.12'
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:4.12.0-SNAPSHOT'

//    testRuntime 'org.slf4j:slf4j-simple:1.7.22'
}
